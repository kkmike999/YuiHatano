apply plugin: 'com.github.dcendents.android-maven'
// https://github.com/bintray/gradle-bintray-plugin
apply plugin: 'com.jfrog.bintray'

// 参考 https://blog.csdn.net/linglongxin24/article/details/53415932
repositories {
    // 仓库地址
    maven { url "https://dl.bintray.com/kkmike999/maven" }
}

// 项目主页
def siteUrl = 'https://github.com/kkmike999/YuiHatano' // 项目主页
// 项目的版本控制地址
def gitUrl = 'https://github.com/kkmike999/YuiHatano.git' // 项目的版本控制地址
def descript = 'Android DAO单元测试框架'
def mArtifactId = project.name

group = 'net.yui' // 发布到组织名称名字，必须填写
version = '1.1.6'

println("\ngroup=${group}, artifactId=${mArtifactId}, version=${version}\n")

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                name mArtifactId
                description descript
                url siteUrl

                packaging 'aar'
                artifactId mArtifactId

                println("install.pom name=${mArtifactId}, artifactId=${artifactId}")

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'kkmike999'
                        name 'kkmike999'
                        email 'kkmike999@163.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到jcenter
bintray {
    user = project.getProperties().get("bintray.user")
    key = project.getProperties().get("bintray.apikey")

    println("bintray user=${user}, key=${key}")

    configurations = ['archives']
    pkg {
        repo = "maven"  // 上传到中央仓库的名称
        name = mArtifactId // 发布到JCenter上的项目名字，必须填写
        desc = descript    //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

/////////////////////////  辅助配置  /////////////////////////

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    options.links "https://coding.net/u/kkmike999/p/bintray-release/git"
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

// 上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}